buildscript {
    ext.kotlin_version = findProperty("deployVersion") ?: "1.1-SNAPSHOT"
    ext.kotlin_language_version = "1.1"
    ext.kotlin_gradle_plugin_version = findProperty("kotlinGradlePluginVersion") ?: "1.1.1"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_gradle_plugin_version}"
    }
}

apply from: 'commonConfiguration.gradle'

ext {
    JDK_16 = jdkPath("1.6")
    JDK_17 = jdkPath("1.7")
    JDK_18 = jdkPath("1.8")
    distDir = file("${rootDir}/../dist")
    distLibDir = file("${rootDir}/../dist/kotlinc/lib")
    bootstrapCompilerFile = file("$distDir/kotlin-compiler-for-maven.jar")
}

allprojects {
    group = 'org.jetbrains.kotlin'
    version = "$kotlin_version"
}

subprojects {

    repositories {
        mavenCentral()
    }

    configurations {
        scriptCompile
        scriptRuntime.extendsFrom(scriptCompile)
    }

    dependencies {
        scriptCompile "org.jetbrains.kotlin:kotlin-compiler-embeddable:${kotlin_gradle_plugin_version}"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile) {
        compilerJarFile = bootstrapCompilerFile
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
    }

}

task clean {
    doLast {
        delete "${buildDir}/repo"
    }
}

if (isSonatypeRelease) {
    println 'Applying configuration for sonatype release'
    apply from: 'prepareSonatypeStaging.gradle'
}